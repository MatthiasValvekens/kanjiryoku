description = 'zinnia-swig'
group = 'org.chasen.crfpp'
version = '0.06-JAVA'

apply plugin: 'java'

task runSwig(type: Exec) {
    doFirst {
        mkdir('build')
        mkdir('src/main/java/org/chasen/crfpp')
    }
    outputs.dirs 'build', 'src/main/java/org/chasen/crfpp'
    commandLine 'swig', '-c++', '-java',
      '-package', 'org.chasen.crfpp',
      '-Isrc/main/include', 
      '-o', 'build/ZinniaJNI.cpp', 
      '-outdir', 'src/main/java/org/chasen/crfpp',
      'src/main/swig/zinnia.i'
}

// TODO: compiling JNI code in a platform-agnostic way is complicated.
// Will deal with this soon (TM).

task compileBridgeObject(type: Exec, dependsOn: runSwig) {
    // g++ -Isrc/main/include -I$(java_home)/include -o build/libjzinnia-0.06-JAVA.o -c build/ZinniaJNI.cpp
    commandLine 'g++', '-Isrc/main/include', "-I${System.env.JAVA_HOME}/include", '-o', 'build/libjzinnia-0.06-JAVA.o', '-c', 'build/ZinniaJNI.cpp'
    outputs.file 'build/libjzinnia-0.06-JAVA.o'
}

task linkBridge(type: Exec) {
     dependsOn compileBridgeObject
     // g++ -dynamiclib build/libjzinnia-0.06-JAVA.o -o build/libjzinnia-0.06-JAVA.jnilib ../libzinnia.0.dylib
     // TODO: for some reason there is a failure at runtime when libzinnia.0.dylib
     // is not in /usr/local/lib, DYLD_LIBRARY_PATH and the like don't seem to affect
     // this. ???
     commandLine 'g++', '-dynamiclib', 'build/libjzinnia-0.06-JAVA.o', '-o', 'build/libjzinnia-0.06-JAVA.jnilib', 'libzinnia.0.dylib'
     outputs.file 'build/libjzinnia-0.06-JAVA.jnilib'
}

dependencies {
    compile files('../libzinnia.0.dylib')
}

compileJava {
    dependsOn runSwig, linkBridge
}
